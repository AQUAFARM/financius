buildscript {
    repositories {
        maven { url 'http://download.crashlytics.com/maven' }
    }

    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

repositories {
    maven { url 'http://download.crashlytics.com/maven' }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId 'com.code44.finance'
        targetSdkVersion 22
        minSdkVersion 16
        versionCode 99
        versionName '0.19.8'
        buildConfigField "boolean", "USE_LOCAL_SERVER", "false"
        buildConfigField "String", "LOCAL_SERVER_IP", "\"" + getLocalIp() + "\""
        buildConfigField "long", "BUILD_TIME", "" + buildTime() + "L"
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
        release {
            buildConfigField "boolean", "USE_LOCAL_SERVER", "false"
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        full {
        }
        noPlay {
        }
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(path: ':common')
    compile(project(path: ':backend', configuration: 'android-endpoints')) {
        exclude(module: 'guava-jdk5')
    }
    compile 'com.android.support:support-annotations:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:support-v13:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.google.guava:guava:18.+'
    compile 'com.crashlytics.android:crashlytics:1.+'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.+'
    compile 'com.squareup.okhttp:okhttp:2.2.+'
    compile 'com.squareup.retrofit:retrofit:1.9.+'
    compile 'com.squareup.picasso:picasso:2.4.+'
    compile 'com.squareup:otto:1.3.+'
    compile 'com.squareup.dagger:dagger:1.2.+'
    compile 'org.beanshell:bsh-core:2.0b4'
    compile 'com.larswerkman:HoloColorPicker:1.4'
    compile 'net.danlew:android.joda:2.7.+'
    compile 'me.grantland:autofittextview:0.2.+'
    compile 'com.nononsenseapps:filepicker:1.1.+'
    compile 'uk.co.chrisjenx:calligraphy:2.0.+'
    compile 'com.getbase:floatingactionbutton:1.6.+'
    compile 'com.github.lecho:hellocharts-library:1.5.4@aar'
    fullCompile 'com.google.android.gms:play-services-base:7+'
    fullCompile 'com.google.android.gms:play-services-gcm:7+'
    fullCompile 'com.google.android.gms:play-services-identity:7+'
    fullCompile 'com.google.android.gms:play-services-analytics:7+'
    fullCompile 'com.google.android.gms:play-services-drive:7+'
    fullCompile 'com.google.android.gms:play-services-plus:7+'
    provided 'com.squareup.dagger:dagger-compiler:1.2.+'
}

def buildTime() {
    return System.currentTimeMillis();
}

def getLocalIp() {
    return InetAddress.getLocalHost().getHostAddress();
}